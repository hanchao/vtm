buildscript {
  repositories {
    mavenCentral()
  }
  dependencies {
    classpath 'com.android.tools.build:gradle:0.9.+'
  }
}

apply plugin: 'android'

dependencies {
  compile project(':vtm-android')
  compile project(':vtm-themes')
}

android {
  compileSdkVersion 19
  buildToolsVersion '19.0.1'

  sourceSets {
    main {
      manifest.srcFile 'AndroidManifest.xml'
      java.srcDirs = ['src']
      resources.srcDirs = ['src', 'assets']
      res.srcDirs = ['res']
      assets.srcDirs = ['assets']
    }

    debug.setRoot('build-types/debug')
    release.setRoot('build-types/release')
  }

  // ignore deprecated
  lintOptions.abortOnError false
}

// Including configurations into Eclipse
eclipse {
  sourceCompatibility = 1.6
  targetCompatibility = 1.6

  // Configuring Eclipse classpath
  classpath {
    plusConfigurations += configurations.compile

    defaultOutputDir = file('bin/classes')

    file {
      // Direct manipulation of the generated classpath XML
      withXml {
	def node = it.asNode()
	node.appendNode('classpathentry kind="src" path="src"')
	node.appendNode('classpathentry kind="src" path="gen"')
      }
    }
  }

  project {
    natures = ['com.android.ide.eclipse.adt.AndroidNature',
	       'org.eclipse.jdt.core.javanature']

    buildCommand 'com.android.ide.eclipse.adt.ResourceManagerBuilder'
    buildCommand 'com.android.ide.eclipse.adt.PreCompilerBuilder'
    buildCommand 'com.android.ide.eclipse.adt.ApkBuilder'
  }
}

task run (dependsOn: 'installDebug'){
  doFirst {
    println(">> adb run...")
    String adb = System.getenv()['ANDROID_HOME'] + '/platform-tools/adb'
    String cmd = "${adb} shell am start -n org.oscim.jeo.android/.TestActivity"
    def proc = cmd.execute()
    proc.in.eachLine {line -> println line}
    proc.err.eachLine {line -> System.err.println( 'ERROR: ' + line)}
    proc.waitFor()
  }
}
